"""Phase 5 for Stage0: Build M2-Planet from cc_x86 

This toolchain is only used once, so we don't turn it
into a full toolchain type
"""

load("//tools/stage0:hex2.bzl", "hex2_binary")
load("//tools/stage0:m0.bzl", "m0_expand")
load("//tools/stage0:catm.bzl", "cat_files")
load("//tools/stage0:cc.bzl", "cc_compile")

package(
    default_visibility = ["//visibility:public"],
)

cat_files(
    name = "M2-0.c",
    srcs = select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                "@M2libc//:x86/linux/bootstrap.c",
            ],
        },
    ) + [
        # do not sort
        "@M2-Planet//:cc.h",
        "@M2libc//:bootstrappable.c",
        "@M2-Planet//:cc_globals.c",
        "@M2-Planet//:cc_reader.c",
        "@M2-Planet//:cc_strings.c",
        "@M2-Planet//:cc_types.c",
        "@M2-Planet//:cc_core.c",
        "@M2-Planet//:cc_macro.c",
        "@M2-Planet//:cc.c",
    ],
)

cc_compile(
    name = "M2-0.M1",
    src = ":M2-0.c",
)

cat_files(
    name = "M2-0-0.M1",
    srcs = select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@stage0-x86//:x86_defs.M1",
                "@stage0-x86//:libc-core.M1",
            ],
        }) + [
            ":M2-0.M1",
        ],
)

m0_expand(
    name = "M2-0.hex2",
    src = ":M2-0-0.M1",
)

hex2_binary(
    name = "M2",
    srcs = select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@stage0-x86//:ELF-i386.hex2",
            ],
        },
    ) + [
        ":M2-0.hex2",
    ],
)