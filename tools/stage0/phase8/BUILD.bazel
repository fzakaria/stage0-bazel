"""Phase 8 for Stage0: Build hex2-1 from C sources 

This is the last stage where we will be using the handwritten hex2 and instead
be using the far more powerful, cross-platform version with a bunch more goodies.
"""

load("//tools/stage0:hex2.bzl", "hex2_binary")
load("//tools/stage0:m1.bzl", "m1_expand")
load("//tools/stage0:m2.bzl", "m2_compile")

package(
    default_visibility = ["//visibility:public"],
)

m2_compile(
    name = "hex2_linker-0.M1",
    srcs = [ 
        # do not sort
        "@M2libc//:sys/types.h",
        "@M2libc//:stddef.h",
        "@M2libc//:sys/utsname.h",
    ] + 
    select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@M2libc//:x86/linux/unistd.c",
                "@M2libc//:x86/linux/fcntl.c",
            ],
        },
    ) + [
        "@M2libc//:fcntl.c",
    ] + select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@M2libc//:x86/linux/sys/stat.c",
            ],
        },
    ) + [
        # do not sort
        "@M2libc//:stdlib.c",
        "@M2libc//:stdio.h",
        "@M2libc//:stdio.c",
        "@M2libc//:bootstrappable.c",
        "@mescc-tools//:hex2.h",
        "@mescc-tools//:hex2_linker.c",
        "@mescc-tools//:hex2_word.c",
        "@mescc-tools//:hex2.c",
    ],
    architecture = select({
        "@bazel_tools//src/conditions:linux_x86_64": "x86",
    }),
)

m1_expand(
    name = "hex2_linker-0.hex2",
    srcs = select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@M2libc//:x86/x86_defs.M1",
                "@M2libc//:x86/libc-full.M1",
            ],
        },
    ) + [
        ":hex2_linker-0.M1",
    ],
    architecture = select({
        "@bazel_tools//src/conditions:linux_x86_64": "x86",
    }),
    is_little_endian = select({
        "@bazel_tools//src/conditions:linux_x86_64": True,
    }),
)

hex2_binary(
    name = "hex2-1",
    srcs = select(
        {
            "@bazel_tools//src/conditions:linux_x86_64": [
                # do not sort
                "@M2libc//:x86/ELF-x86.hex2",
            ],
        },
    ) + [
        ":hex2_linker-0.hex2",
    ],
)
